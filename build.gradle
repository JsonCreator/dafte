apply plugin: 'java'

repositories {
    mavenCentral()
}
configurations {
    invoker
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

dependencies {
    // Build
    implementation group: 'com.google.cloud.functions', name: 'functions-framework-api', version: gcFunctionsVersion
    implementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
    implementation group: 'com.google.guava', name: 'guava', version: guavaVersion


    // Run Invoker
    invoker group: 'com.google.cloud.functions.invoker', name: 'java-function-invoker', version: gcFunctionsInvokerVersion

    // Tests
    testImplementation group: 'com.google.cloud.functions', name: 'functions-framework-api', version: gcFunctionsVersion
}

// Register a "runFunction" task to run the function locally
tasks.register("runFunction", JavaExec) {
    main = 'com.google.cloud.functions.invoker.runner.Invoker'
    classpath(configurations.invoker)
    inputs.files(configurations.runtimeClasspath, sourceSets.main.output)
    args(
            '--target', project.findProperty('run.functionTarget') ?: '',
            '--port', project.findProperty('run.port') ?: 8080
    )
    doFirst {
        args('--classpath', files(configurations.runtimeClasspath, sourceSets.main.output).asPath)
    }
}
